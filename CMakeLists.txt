cmake_minimum_required(VERSION 3.13)

project(qmex)
set(PACKAGE_NAME ${PROJECT_NAME})

set(CMAKE_CXX_STANDARD 11)
set(CMAKE_POSITION_INDEPENDENT_CODE ON)
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin)

add_library(${PROJECT_NAME} qmex.cpp)
add_library(${PACKAGE_NAME}::${PROJECT_NAME} ALIAS ${PROJECT_NAME})
target_include_directories(${PROJECT_NAME} PUBLIC $<BUILD_INTERFACE:${PROJECT_SOURCE_DIR}>)

get_target_property(PROJECT_LIBRARY_TYPE ${PROJECT_NAME} TYPE)
string(TOUPPER ${PROJECT_NAME} PROJECT_UPPER_NAME)
string(MAKE_C_IDENTIFIER ${PROJECT_UPPER_NAME} PROJECT_UPPER_NAME)
set_target_properties(${PROJECT_NAME} PROPERTIES DEFINE_SYMBOL ${PROJECT_UPPER_NAME}_EXPORTS)
target_compile_definitions(${PROJECT_NAME} PUBLIC ${PROJECT_UPPER_NAME}_${PROJECT_LIBRARY_TYPE})


project(${PACKAGE_NAME}-cli)

add_executable(${PROJECT_NAME} cli.cpp)
add_executable(${PACKAGE_NAME}::cli ALIAS ${PROJECT_NAME})

target_link_libraries(${PROJECT_NAME} PUBLIC ${PACKAGE_NAME}::${PACKAGE_NAME})


include(CTest)
if(BUILD_TESTING)
    add_subdirectory(test)
endif()
